#include "repeat.cogent"
#include "wordarray.cogent"

log2stop: StopF (U64, U64) U64
log2stop #{acc, obsv} =
    let (a, b) = acc
    in a >= obsv

log2step: StepF (U64, U64) U64
log2step #{acc, obsv} = 
    let (a, b) = acc
    in (a * 2, b + 1)

mylog2: U64 -> U64
mylog2 n =
    let args = #{n = n, stop = log2stop, step = log2step, acc = (1, 0), obsv = n}
    and (a, b) = repeat args
    in b

expstop: StopF U32 U32
expstop x = False

expstep: StepF U32 U32
expstep #{acc, obsv} = acc * obsv

myexp: (U32, U32) -> U32
myexp (a, b) =
    let args = #{n = upcast b, stop = expstop, step = expstep, acc = 1, obsv = a}
    in repeat args


searchNext : StepF (U32, U32) (WordArray U32, U32)
searchNext #{acc, obsv} =
    let (l, r) = acc
    and (arr, v) = obsv
    and m = l + (r - l) / 2
    and args = #{arr = arr, idx = m, val = 0}
    and x = wordarray_get args
    in 
        if | x < v -> (m + 1, r)
           | else -> (l, m)

searchStop : StopF (U32, U32) (WordArray U32, U32)
searchStop #{acc, obsv} =
    let (l, r) = acc
    and (arr, v) = obsv
    and len = wordarray_length arr
    and args = #{arr = arr, idx = l, val = 0}
    and x = wordarray_get args
    in 
        if | l >= r -> True
           | else -> False

binarySearch : ((WordArray U32)!, U32) -> U32
binarySearch (arr, v) =
	let len = wordarray_length arr
	and args = #{n = upcast len, stop = searchStop, step = searchNext, acc = (0, len), obsv = (arr, v)}
	and (l, r) = repeat args
	in l
